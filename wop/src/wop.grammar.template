Program -> Program Declaration Sep.
Program -> Declaration Sep.

Declaration -> TokenDecl.
Declaration -> UseDecl.
Declaration -> RuleDecl.

TokenDecl -> TokenWord StrLit IdentPath.
TokenDecl -> TokenWord Ident IdentPath.

IdentPath -> IdentPath PathAccess Ident.
IdentPath -> Ident.

UseDecl -> UseWord IdentPath.

AssignOp -> VarSpec.
AssignOp -> RepSpec.
AssignOp -> OptSpec.
AssignOp -> NormalSpec.

AttrPrefix -> MetaAttr.
AttrPrefix -> BoxAttr.
AttrPrefix -> MetaAttr AttrPrefix.
AttrPrefix -> BoxAttr AttrPrefix.

AttrSuffix -> Opt.
AttrSuffix -> Var.
AttrSuffix -> Rep.

VarPipe -> Type Ident.

TypeDecl -> Type IdentPath.

ElmBase -> Ident VarPipe.
ElmBase -> Ident.
ElmBase -> OpenParen RulePipe CloseParen.
ElmBase -> OpenParen RulePipe CloseParen VarPipe.

Elm -> AttrPrefix ElmBase AttrSuffix.
Elm -> ElmBase AttrSuffix.
Elm -> AttrPrefix ElmBase.
Elm -> ElmBase.

Prod -> Prod Elm.
Prod -> Prod Elm CodeExpr.
Prod -> Elm CodeExpr.
Prod -> Elm.

RulePipeRepeater -> RulePipeRepeater Prod Or.
RulePipeRepeater -> Prod Or.

RulePipe -> RulePipeRepeater Prod.
RulePipe -> Prod.

RuleDecl -> Ident TypeDecl AssignOp RulePipe.
RuleDecl -> Ident AssignOp RulePipe.
